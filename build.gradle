plugins{
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'maven'
}

group = 'io.homecentr'

sourceCompatibility = 1.8

jar {
    baseName = 'testcontainers-extensions'
}

repositories{
    mavenCentral()
    jcenter()
}

dependencies{
    implementation "org.testcontainers:testcontainers:1.14.2"

    testImplementation group: 'junit', name: 'junit', version: '4.13'

    testCompile "org.mockito:mockito-core:3.3.3"
}

task sourcesJar(type: Jar, dependsOn: classes){
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc){
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts{
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/homecentr/testcontainers-extensions")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        TestContainersExtensions(MavenPublication){
            from components.java
            groupId group
            artifactId 'testcontainers-extensions'
            version String.valueOf(System.getenv("RELEASE_VERSION")).replace("v", "")
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

test {
    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}