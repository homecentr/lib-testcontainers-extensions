plugins{
    id 'java-library'
    id 'idea'
    id 'maven-publish'
}

group = 'io.homecentr'

sourceCompatibility = 1.8

jar {
    baseName = 'testcontainers-extensions'
}

repositories{
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'org.jetbrains', name: 'annotations', version: '16.0.2'

    implementation group: 'org.testcontainers', name: 'testcontainers', version: '1.15.3'

    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation group: 'org.mockito', name: 'mockito-core', version :'3.4.6'
}

task sourcesJar(type: Jar, dependsOn: classes){
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc){
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts{
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/homecentr/lib-testcontainers-extensions")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
            groupId group
            artifactId 'testcontainers-extensions'
            version String.valueOf(System.getenv("RELEASE_VERSION")).replace("v", "")
        }
    }
}

test {
    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}